plugins {
    id 'application'
    id "org.beryx.jlink" version "2.22.3"
}

group 'com.opengg'
version '0.1.1'

sourceCompatibility = 1.15
targetCompatibility = 1.15

mainClassName = 'com.opengg.modmanager.TTModManager'

java {
    modularity.inferModulePath = true
}

compileJava {
    options.incremental = true
    options.fork = true
}

tasks.withType(JavaCompile) {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath, '--enable-preview'
        ]
        classpath = files()
    }
}

tasks.withType(JavaExec){
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath, '--enable-preview'
        ]
        classpath = files()
    }
}

application  {
    mainModule = 'com.opengg.modmanager'
}

ext.os = org.gradle.internal.os.OperatingSystem.current()

ext.artifactName = 'TTModManager'

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher{
        name = artifactName
        jvmArgs = ['--enable-preview']
        //unixScriptTemplate = file('unixTemplate.txt')
       // windowsScriptTemplate = file('windowsTemplate.txt')
    }
    jpackage {
        if(os.windows) {
            installerOptions = ['--win-per-user-install', '--win-dir-chooser', '--win-menu']
        } else {
            installerType = 'deb' // 'rpm'
        }
        installerOptions += '--verbose'

        //icon = 'dist\\resources\\tex\\icon.ico'
    }
}

task packageDistribution(type: Zip) {
    archiveFileName = artifactName + ".zip"
    destinationDirectory = file("$buildDir/dist")

    from("$buildDir/jpackage/")
}

tasks.jlink.finalizedBy(packageDistribution)


repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.lingala.zip4j:zip4j:2.6.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}